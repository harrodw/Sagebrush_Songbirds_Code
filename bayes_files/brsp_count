
  model{
    #Fixed effect Priors ----
    beta_burn ~ dnorm(0, 0.001) #Offset for burned or not
    beta_fire_y ~ dnorm(0, 0.001) #offset for years since fire
    beta_burn_sev ~ dnorm(0, 0.001) #offset for fire sevarity
    beta_elev ~ dnorm(0, 0.001) #offset for elevation
    
    
    sigma_route ~ dunif(0, 50) #between route variation
    sigma2_route <- pow(sigma_route, 2) 
    tau_route <- pow(sigma_route, -2)
    pd ~ dunif(0, 1) #probability of detection
    
    #Random intercept priors ---
    for(n in 1:n_routes){
      beta0[route[n]] ~ dnorm(0, tau_route)
    } #end random effects loop
    
    #State process Likelihood -----
    for(k in 1:K){ 
      lin_comb[k] <- beta0[route_visits[k]] +
                     beta_burn * burn_yn[k] + 
                     beta_fire_y * fire_y[k] * burn_yn[k] + 
                     beta_burn_sev * burn_sev[k] * burn_yn[k] +
                     beta_elev * elevation[k]
      #linear combination of predictors
      lambda[k] <- exp(lin_comb[k]) #log link
      n_brsp[k] ~ dpois(lambda[k]) 
    } 
    
    #Observation process Likelihood -----
     for(k in 1:K){
       obs_brsp[k] ~ dbin(pd, n_brsp[k])
     }
     
  }# end model

